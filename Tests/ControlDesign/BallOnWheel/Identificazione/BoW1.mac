/* Coordinate indipendenti e derivate */
var:[phi_w, phi_b, w_w, w_b];
depends(var,t);
inp:[T_w];
depends(inp,t);

dphi_w:diff(phi_w,t);
dphi_b:diff(phi_b,t);
ddphi_w:diff(phi_w,t,2);
ddphi_b:diff(phi_b,t,2);

/* Variabili ausiliarie */
/* Centro massa palla*/
phi_0: (phi_w*R_w+phi_b*R_b)/(R_w+R_b);

/* Posizione x centro di massa palla  */
x_b:(R_w+R_b)*sin(phi_0);
y_b:(R_w+R_b)*cos(phi_0);

/* Derivate per il calcolo */
dx_b:diff(x_b,t);
dy_b:diff(y_b,t);

/* Energie */
T:1/2*J_w*dphi_w^2+1/2*J_b*dphi_b^2+1/2*M_b*(dx_b^2+dy_b^2);

U:M_b*g*y_b;
L:T-U;
D:1/2*d_w*dphi_w^2;

/* Equazioni  di Lagrange */
eqn1:diff(diff(L,dphi_w),t)-diff(L,phi_w)+ diff(D,dphi_w)=T_w;
eqn2:diff(diff(L,dphi_b),t)-diff(L,phi_b)=0;

eqn1:expand(eqn1);
eqn2:expand(eqn2);
eqn1:trigreduce(eqn1);
eqn2:trigreduce(eqn2);
eqn1:trigsimp(eqn1);
eqn2:trigsimp(eqn2);

/* Equazioni differenziali */
sol:linsolve([eqn1,eqn2],[ddphi_w,ddphi_b]);
s1:rhs(sol[1]);
s2:rhs(sol[2]);

s1:subst([diff(phi_w,t)=w_w,diff(phi_b,t)=w_b],s1);
s2:subst([diff(phi_w,t)=w_w,diff(phi_b,t)=w_b],s2);

syseq:[w_w,
       w_b,
       s1,
       s2];

A:jacobian(syseq,var);
B:jacobian(syseq,inp);

A:at(A,[phi_w=0,w_w=0,phi_b=0,w_b=0,T_w=0]);
A:ratsimp(A);
B:at(B,[phi_w=0,w_w=0,phi_b=0,w_b=0,T_w=0]);
B:ratsimp(B);
