kill(all);
/* Coordinate indipendenti e derivate */
var:[phi_w, phi_2, omega_w, omega_2];
depends(var,t);
inp:[T_w];
depends(inp,t);

dphi_w:diff(phi_w,t);
dphi_2:diff(phi_2,t);
ddphi_w:diff(phi_w,t,2);
ddphi_2:diff(phi_2,t,2);

/* Variabili ausiliarie */
/* Centro massa palla*/
R_2: R_w+R_b;
phi_b: (R_2*phi_2-R_w*phi_w)/R_b;

w_b : diff(phi_b,t);
y_b: R_2*cos(phi_2);

/* Energie */
T:1/2*J_w*dphi_w^2+1/2*J_b*w_b^2+1/2*M_b*dphi_2^2;

U:M_b*g*y_b;
L:T-U;
D:1/2*d_w*dphi_w^2;
D:0;

/* Equazioni  di Lagrange */
eqn1:diff(diff(L,dphi_w),t)-diff(L,phi_w) + diff(D,dphi_w)=T_w;
eqn2:diff(diff(L,dphi_2),t)-diff(L,phi_2)=0;

eqn1:expand(eqn1);
eqn2:expand(eqn2);
eqn1:trigreduce(eqn1);
eqn2:trigreduce(eqn2);
eqn1:trigsimp(eqn1);
eqn2:trigsimp(eqn2);

/* Equazioni differenziali */
sol:linsolve([eqn1,eqn2],[ddphi_w,ddphi_2]);
s1:rhs(sol[1]);
s2:rhs(sol[2]);

s1:subst([diff(phi_w,t)=omega_w,diff(phi_2,t)=omega_2],s1);
s2:subst([diff(phi_w,t)=omega_w,diff(phi_2,t)=omega_2],s2);

syseq:[omega_w,
       omega_2,
       s1,
       s2];

A:jacobian(syseq,var);
B:jacobian(syseq,inp);

A:at(A,[phi_w=0,omega_w=0,phi_2=0,omega_2=0,T_w=0]);
A:ratsimp(A);
B:at(B,[phi_w=0,omega_w=0,phi_2=0,omega_2=0,T_w=0]);
B:ratsimp(B);
