kill(all);

/* Coordinate indipendenti e derivate */
var:[phi_w, phi_b, omega_w, omega_b];
depends(var,t);
inp:[T_w];
depends(inp,t);

dphi_w:diff(phi_w,t);
dphi_b:diff(phi_b,t);
ddphi_w:diff(phi_w,t,2);
ddphi_b:diff(phi_b,t,2);

/* Variabili ausiliarie */
/* Centro massa palla*/
phi_2: (phi_w*R_w+phi_b*R_b)/R_w;

/* Posizione x centro di massa palla  */
R_2: R_w+R_b;
w_b : dphi_b;

dphi_2:diff(phi_2,t);
y_b:R_2*cos(phi_2);
v_cm: R_2*dphi_2;

/* Energie */
T:1/2*J_w*dphi_w^2+1/2*J_b*dphi_b^2+1/2*M_b*v_cm^2;

U:M_b*g*y_b;
L:T-U;
D:1/2*d_w*dphi_w^2;

/* Equazioni  di Lagrange */
eqn1:diff(diff(L,dphi_w),t)-diff(L,phi_w)+diff(D,dphi_w)=T_w;
eqn2:diff(diff(L,dphi_b),t)-diff(L,phi_b)=0;

eqn1:expand(eqn1);
eqn2:expand(eqn2);
eqn1:trigreduce(eqn1);
eqn2:trigreduce(eqn2);
eqn1:trigsimp(eqn1);
eqn2:trigsimp(eqn2);

/* Equazioni differenziali */
sol:linsolve([eqn1,eqn2],[ddphi_w,ddphi_b]);
s1:rhs(sol[1]);
s2:rhs(sol[2]);

s1:subst([diff(phi_w,t)=w_w,diff(phi_b,t)=w_b],s1);
s2:subst([diff(phi_w,t)=w_w,diff(phi_b,t)=w_b],s2);
s3:(R_w*s1+R_b*s2)/R_w;

s1:ratsimp(s1);
s3:ratsimp(s3);





